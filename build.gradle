apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

// --- JAVADOC ---

javadoc {
	options.encoding = 'UTF-8'
	failOnError = false
}

// --- CONFIGURATIONS ---

configurations {
    runtime
    ecj
}

configurations.all {
    // resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// --- REPOSITORIES ---

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

// --- DEPENDENCIES ---

dependencies {
    testCompile 'junit:junit:4.12'
	ecj 'org.eclipse.jdt.core.compiler:ecj:4.4.2'
        
    // --- LOGGING ---
    
	// http://mvnrepository.com/artifact/org.slf4j/slf4j-api
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28'
       
	// http://mvnrepository.com/artifact/org.slf4j/slf4j-jdk14
	compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.28'

	// https://mvnrepository.com/artifact/org.slf4j/log4j-over-slf4j
	compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.28'

	// https://mvnrepository.com/artifact/org.slf4j/jcl-over-slf4j
	compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.28'
    
    // --- MOLECULER CORE ---
    	    
	// https://mvnrepository.com/artifact/com.github.berkesa/moleculer-java
	// compile project(':moleculer-java')
	compile group: 'com.github.berkesa', name: 'moleculer-java', version: '1.2.4'
		
	// --- ANSI TERMINAL ---
	
	// https://mvnrepository.com/artifact/com.diogonunes/JCDP
	compile group: 'com.diogonunes', name: 'JCDP', version: '2.0.3.1'

}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.github.berkesa'

// version = '1.2.1-SNAPSHOT'
version = '1.2.1'

modifyPom {
    project {
        artifactId 'moleculer-java-repl'
        name 'Moleculer REPL Module'
        description 'Java implementation of REPL module for Moleculer framework.'
        url 'https://moleculer-java.github.io/moleculer-java-repl/'
        inceptionYear '2018'

        scm {
            url 'https://moleculer-java.github.io/moleculer-java-repl/'
            connection 'scm:https://github.com/moleculer-java/moleculer-java-repl.git'
            developerConnection 'scm:git://github.com/moleculer-java/moleculer-java-repl.git'
        }

        licenses {
            license {
                name "The MIT License"
                url "http://www.opensource.org/licenses/MIT"
                distribution 'repo'
            }
        }
        
        dependencies {
            dependency {
                groupId 'com.github.berkesa'
                artifactId 'moleculer-java'
                version '1.2.4'
                scope 'runtime'
            }
            dependency {
                groupId 'com.diogonunes'
                artifactId 'JCDP'
                version '2.0.3.1'
                scope 'runtime'
            }            
        }

        developers {
            developer {
                id 'berkesa'
                name 'Andras Berkes'
                email 'andras.berkes@programmer.net'
            }
        }
    }
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
	snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

// --- COMPILATION ---

compileJava {
	options.fork = true
	options.encoding = 'UTF-8'
	options.forkOptions.with {
		executable = 'java'
		jvmArgs = ['-classpath', project.configurations.ecj.asPath, 'org.eclipse.jdt.internal.compiler.batch.Main', '-nowarn']
	}
}

// --- JAR ---

jar {
    baseName = 'moleculer-repl'
    version =  '1.2.1'
}